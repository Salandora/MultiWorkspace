buildscript {
    repositories {
        maven { url = "https://maven.fabricmc.net/" }
    }
}

plugins {
    id "fabric-loom" version "1.5-SNAPSHOT"

    id "io.github.p03w.machete" version "2.0.1" apply false // automatic jar compressing on build
}

group = project.mod_group_id

@SuppressWarnings('unused')
def moduleDependency(Project project, List<String> depNames) {
    def deps = depNames.iterator().collect { project.dependencies.project(path: ":$it", configuration: "namedElements") }

    project.dependencies {
        deps.each {
            api it
        }
    }
}

configurations.configureEach {
    resolutionStrategy {
        // fixes loom using a loader version from a dependency
        force("net.fabricmc:fabric-loader:$project.loader_version")
        // this is necessary cause the 1.19.2 REI version somehow provides a version that is not for MC 1.19.2
        force("dev.architectury:architectury-fabric:$project.architectury_version")
    }
}

allprojects  {
    apply plugin: "fabric-loom"

    base {
        archivesName = "${mod_id}"
    }
    version = "${mod_version}+mc${project.minecraft_version}${getStable()}${getBuildNumber()}"

    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 17
    }

    java {
        withSourcesJar()
    }

    repositories {
        mavenLocal()
        mavenCentral()

        maven { url = "https://maven.quiltmc.org/repository/release" } // QM

        maven { url = "https://maven.fabricmc.net/" } // FAPI, Loader
        maven { url = "https://mvn.devos.one/snapshots/" } // serialization-hooks
        maven { url = "https://jitpack.io/" } // for Porting Lib: Mixin Extras, Fabric ASM
        maven { // Forge Config API Port
            name = "Fuzs Mod Resources"
            url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
        }
        maven { // Modern KeyBinding
            url = "https://maven.nova-committee.cn/releases"
        }

        maven { url = "https://maven.terraformersmc.com/releases/" } // Emi
        maven { url = "https://maven.shedaniel.me/" } // REI and deps
        exclusiveContent {
            forRepository {
                maven {
                    name = "Modrinth"
                    url = "https://api.modrinth.com/maven"
                }
            }
            filter {
                includeGroup "maven.modrinth"
            }
        }
        maven {
            name = "entity reach"
            url = "https://maven.jamieswhiteshirt.com/libs-release/"
        }

        maven { url = 'https://maven.blamejared.com' }
        maven { url = "https://modmaven.dev" } // Jei
        maven { url = "https://maven.ladysnake.org/releases" } // CCA, for Trinkets
        maven { url = "https://www.cursemaven.com" } // Balm, CraftingTweaks
        maven { url = "https://maven.resourcefulbees.com/repository/terrarium/" } // Chipped, resourcefullib
    }

    dependencies {
        minecraft("com.mojang:minecraft:$project.minecraft_version")
        mappings(loom.layered {
            it.mappings("org.quiltmc:quilt-mappings:$project.minecraft_version+build.$project.qm_version:intermediary-v2")
            it.officialMojangMappings() { nameSyntheticMembers = false }
        })

        modImplementation("net.fabricmc:fabric-loader:$project.loader_version")
        modImplementation("net.fabricmc.fabric-api:fabric-api:$project.fabric_version")

        implementation("javax.annotation:javax.annotation-api:1.3.2")
        implementation("com.google.code.findbugs:jsr305:3.0.2")

        modImplementation("committee.nova.mkb.fabric:mkb-${project.minecraft_version}:${project.mkb_mod_version}") { transitive = false }

        modImplementation("com.terraformersmc:modmenu:$project.modmenu_version")
        modImplementation("maven.modrinth:lazydfu:$project.lazy_dfu_version")

        // Test dependencies
        testImplementation "net.fabricmc:fabric-loader-junit:${project.loader_version}"
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.+'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.+'
        testImplementation "org.mockito:mockito-core:5.10.+"
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.+'
    }

    loom {
        runtimeOnlyLog4j = true

        runs.configureEach {
            vmArg("-Dmixin.debug.export=true") // export all mixins
            vmArg("-Dmixin.debug.strict.unique=true") // strictly enforce that @Unique things are private
            vmArg("-enableassertions")
        }

        runs {
            client {
                ideConfigGenerated = true
            }

            server {
                ideConfigGenerated = true
            }
        }
    }

    subprojects.each { p ->
        loom.mods.register(p.name) {
            sourceSet p.sourceSets.main
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }

        workingDir "run"
    }
    test.dependsOn("processResources")

    tasks.withType(ProcessResources).configureEach {
        exclude '.cache'

        var replaceProperties = [
                mod_id              : mod_id,
                mod_version         : version,
                minecraft_version   : minecraft_version,
                loader_version      : loader_version,
                fabric_version      : fabric_version,
                sc_version          : project(":SophisticatedCore").version,
                mkb_version         : mkb_mod_version,
                forge_config_version: forge_config_api_port_version,
                energy_version      : energy_version,
                port_lib_version    : port_lib_version
        ]

        inputs.properties replaceProperties

        filesMatching(['fabric.mod.json', 'pack.mcmeta']) {
            expand replaceProperties + [project: project]
        }
    }
}

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "io.github.p03w.machete"

    dependencies {
        implementation(include(annotationProcessor("io.github.llamalad7:mixinextras-fabric:$project.mixinextras_version")))

        for (def module in port_lib_modules.split(',')) {
            modApi(include("io.github.fabricators_of_create.Porting-Lib:${module.trim()}:$port_lib_version+$minecraft_version"))
        }

        // EMI
        modCompileOnly("dev.emi:emi-fabric:$emi_version")
        // JEI
        modCompileOnlyApi("mezz.jei:jei-$minecraft_version-common-api:$jei_version")
        modCompileOnly("mezz.jei:jei-$minecraft_version-fabric:$jei_version")
        // REI
        modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:$rei_version")
        modCompileOnly("me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:$rei_version")

        /*modCompileOnly("maven.modrinth:sodium:mc${minecraft_version}-${sodium_version}")
        modCompileOnly("maven.modrinth:indium:${indium_version}+mc${minecraft_version}")*/

        implementation("com.electronwill.night-config:core:$project.night_config_version")
        implementation("com.electronwill.night-config:toml:$project.night_config_version")
        modApi(include("net.minecraftforge:forgeconfigapiport-fabric:$project.forge_config_api_port_version"))
        modApi(include("teamreborn:energy:$project.energy_version")) { transitive = false }

        modCompileOnly("me.shedaniel.cloth:cloth-config-fabric:$project.cloth_config_version")

        modCompileOnly("maven.modrinth:jade:${jade_version}")

        modCompileOnly("curse.maven:malilib-303119:$project.malilib_cf_file_id")
        modCompileOnly("curse.maven:litematica-308892:$project.litematica_cf_file_id")

        modCompileOnly("curse.maven:balm-500525:${project.balm_cf_file_id}")
        modCompileOnly("curse.maven:craftingtweaks-502516:${project.crafting_tweaks_cf_file_id}")

        modCompileOnly("curse.maven:chipped-456956:${project.chipped_cf_file_id}")
        modCompileOnly("curse.maven:resourcefullib-570073:${project.resourcefullib_cf_file_id}")

        modCompileOnly("vazkii.botania:Botania:${project.botania_version}") { transitive = false }
    }

    fabricApi {
        configureDataGeneration()
    }

    loom {
        runs {
            datagen {
                ideConfigGenerated = true
                vmArg "-Dfabric-api.datagen.modid=$mod_id"
            }
        }
    }

    tasks.register("generateResources") {
        group = "fabric"
        generateResources.dependsOn runDatagen
    }

    machete {
//    enabled = System.getenv("GITHUB_RUN_NUMBER") != null
        json.enabled = false
        finalizeAfter = ""
        ignoredTasks.add("jar")
        additionalTasks.add("remapJar")
    }

    tasks.remapJar.finalizedBy('optimizeOutputsOfRemapJar')

    tasks.register('optimizedJar') {
        dependsOn 'optimizeOutputsOfRemapJar'
    }
    tasks.build.dependsOn optimizedJar

    tasks.named('jar', Jar).configure {
        from("LICENSE") {
            rename { "${it}_${base.archivesName.get()}"}
        }

        manifest {
            attributes([
                    'Specification-Title'     : mod_id,
                    'Specification-Vendor'    : "p3pp3rf1y",
                    'Specification-Version'   : '1', // We are version 1 of ourselves
                    'Implementation-Title'    : name,
                    'Implementation-Version'  : jar.archiveVersion,
                    'Implementation-Vendor'   : "p3pp3rf1y",
                    'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
            ])
        }
    }

    tasks.register('printVersionName') {
        println "version: " + version
    }
}

def additionalMods(DependencyHandler deps) {
    switch (recipe_viewer.toLowerCase(Locale.ROOT)) {
        case "emi": deps.modLocalRuntime("dev.emi:emi-fabric:$project.emi_version"); println "EMI loaded"; break
        case "jei": deps.modLocalRuntime("mezz.jei:jei-$project.minecraft_version-fabric:$project.jei_version"); println "JEI loaded"; break
        case "rei": deps.modLocalRuntime("me.shedaniel:RoughlyEnoughItems-fabric:$project.rei_version"); println "REI loaded"; break
        case "disabled": break
        default: println("Unknown recipe viewer specified: $project.recipe_viewer. Must be JEI, REI, EMI, or disabled.")
    }

    deps.modLocalRuntime("org.joml:joml:1.10.4")
    deps.modLocalRuntime("maven.modrinth:indium:${indium_version}+mc${minecraft_version}")
    deps.modLocalRuntime("maven.modrinth:sodium:mc${minecraft_version}-${sodium_version}")

    // Create - dependencies are added transitively
    deps.modLocalRuntime("com.simibubi.create:create-fabric-${project.minecraft_version}:${project.create_version}")

    deps.modLocalRuntime("dev.emi:trinkets:$project.trinkets_version")
    deps.modLocalRuntime("dev.onyxstudios.cardinal-components-api:cardinal-components-base:$project.cca_version") // for Trinkets
    deps.modLocalRuntime("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:$project.cca_version") // for Trinkets

    deps.modLocalRuntime("vazkii.botania:Botania:${project.botania_version}") { exclude(module: "emi") }
    deps.modLocalRuntime("vazkii.patchouli:Patchouli:${project.patchouli_version}")

    deps.modLocalRuntime("me.shedaniel.cloth:cloth-config-fabric:$project.cloth_config_version")

    deps.modLocalRuntime("maven.modrinth:jade:${jade_version}")

    deps.modLocalRuntime("curse.maven:malilib-303119:$project.malilib_cf_file_id")
    deps.modLocalRuntime("curse.maven:litematica-308892:$project.litematica_cf_file_id")

    deps.modLocalRuntime("curse.maven:balm-500525:${project.balm_cf_file_id}")
    deps.modLocalRuntime("curse.maven:craftingtweaks-502516:${project.crafting_tweaks_cf_file_id}")

    deps.modLocalRuntime("curse.maven:chipped-456956:${project.chipped_cf_file_id}")
    deps.modLocalRuntime("curse.maven:resourcefullib-570073:${project.resourcefullib_cf_file_id}")
}

dependencies {
    afterEvaluate {
        subprojects.each {
            api project(path: "${it.path}", configuration: "namedElements")
        }
    }

    // additionalMods(dependencies)
}

static def getBuildNumber() {
    if (System.getenv("GITHUB_RUN_NUMBER") != null) {
        return "-build." + System.getenv("GITHUB_RUN_NUMBER").toString()
    }
    return "-local"
}

static def getStable() {
    if (System.getenv("GITHUB_REF") == null || System.getenv("GITHUB_REF").endsWith("-dev")) {
        return "-SNAPSHOT"
    }
    return ""
}
